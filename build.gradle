plugins {
	id "de.abas.esdk" version "0.11.3"
	id 'com.github.kt3k.coveralls' version '2.8.2'
}

repositories {
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME-SNAPSHOT" }
	maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME" }
	maven { url "https://registry.abas.sh/artifactory/abas.maven-public/" }
}

esdk {
	app {
		name = "g30l0"
		vendorId = "ag"
		appId = "g30l0"
		shared = true
		infosystems = [ "IS.OW1.G30L0" ]
		tables = [ "Kunde" ]
		screens = [:]
		data = []
		enums = []
		namedTypes = []
		languages = "DA"
		essentialsVersions = [ "2017r4n00-2017r4n17" ]
	}

	abas {
		homeDir = ABAS_HOMEDIR
		clientDir = ABAS_CLIENTDIR
		clientId = ABAS_CLIENTID
		edpHost = EDP_HOST
		edpPort = EDP_PORT.toInteger()
		edpUser = EDP_USER
		edpPassword = EDP_PASSWORD
	}

	nexus {
		nexusHost = NEXUS_HOST
		nexusPort = NEXUS_PORT.toInteger()
		nexusRepoName = NEXUS_NAME
		nexusPassword = NEXUS_PASSWORD
		nexusVersion = NEXUS_VERSION
	}

	ssh {
		host = SSH_HOST
		port = SSH_PORT.toInteger()
		user = SSH_USER
		password = SSH_PASSWORD
		key = SSH_KEY
	}
	installType = "SSH"
}

task instrument(dependsOn: [classes, project.configurations.jacocoAnt]) {

	inputs.files classes.outputs.files
	File outputDir = new File(project.buildDir, 'instrumentedClasses')
	outputs.dir outputDir
	doFirst {
		project.delete(outputDir)
		ant.taskdef(
				resource: 'org/jacoco/ant/antlib.xml',
				classpath: project.configurations.jacocoAnt.asPath,
				uri: 'jacoco'
		)
		def instrumented = false
			if (file(sourceSets.main.java.outputDir).exists()) {
				def instrumentedClassedDir = "${outputDir}/${sourceSets.main.java}"
				ant.'jacoco:instrument'(destdir: instrumentedClassedDir) {
					fileset(dir: sourceSets.main.java.outputDir, includes: '**/*.class')
				}
				//Replace the classes dir in the test classpath with the instrumented one
				sourceSets.test.runtimeClasspath -= files(sourceSets.main.java.outputDir)
				sourceSets.test.runtimeClasspath += files(instrumentedClassedDir)
				instrumented = true
			}
		if (instrumented) {
			//Disable class verification based on https://github.com/jayway/powermock/issues/375
			test.jvmArgs += '-noverify'
		}
	}
}
test.dependsOn instrument

test {
	jacoco {
		excludes += ['**/*']
	}
}

coveralls {
	jacocoReportPath 'build/reports/jacoco/calculateCodeCoverage/calculateCodeCoverage.xml'
}

calculateCodeCoverage {
	reports {
		xml.enabled true
	}
	finalizedBy tasks.coveralls
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(it).matching {
				include "de/abas/esdk/g30l0/**/*.class"
			}
		})
	}
}

codeCoverageVerification {
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(it).matching {
				include "de/abas/esdk/g30l0/**/*.class"
			}
		})
	}
}

group = 'de.abas.esdk.g30l0'

configurations { compile.extendsFrom provided }

dependencies {
	provided 'de.abas.homedir:log4j:1.0.0'

	compile "fr.dudie:nominatim-api:3.3"

	licensing "de.abas.esdk:client-api:0.0.7:all"

	testCompile "junit:junit:4.12"
	testCompile "org.hamcrest:hamcrest-all:1.3"
	testCompile "org.powermock:powermock-module-junit4:1.6.2"
	testCompile "org.powermock:powermock-api-mockito:1.6.2"
}
